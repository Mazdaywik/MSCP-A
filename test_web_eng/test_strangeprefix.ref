/*
 Функция Prefix проверяет, является ли ее первый аргумент префиксом второго, в алфавите {'A','B'}.
 Функция Concat --- приписывание, реализованное с конца.
 Задача суперкомпилятора --- проверить, всегда ли с его помощью получается префикс.
*/

/***********************************************/
/*          исходная программа                 */
/***********************************************/

$ENTRY Go {
	 (e.x) e.y = <Prefix (e.x) <Concat (e.x) e.y>>;
}

Prefix {
	() e.2 = 'T';
	(t.x e.1) t.x e.2 = <Prefix (e.1) e.2>;
	('A' e.1) 'B' e.2 = 'F';
	('B' e.1) 'A' e.2 = 'F';
	(t.x e.1) = 'F';
}

Concat {
	(e.1) = e.1;
	(e.1) e.2 t.1 = <Concat (e.1) e.2> t.1;
}

/***********************************************/
/*          остаточная программа               */
/***********************************************/
/*
$ENTRY Go {
 (e.1) e.2 =  <InputFormat_0 (e.1) (e.2)>;
}


InputFormat_0 {
 () (e.1) =  'T';
 (t.1 e.1) (e.2) =  <Prefix_0 (t.1) (e.1) (e.2) ()>;
}


Prefix_12_0 {
 () (e.1) =  'T';
 (t.1 e.1) (e.2) =  <Prefix_12_0 (e.1) (e.2)>;
}


Prefix_0 {
 (t.1) (e.1) () (e.2) =  <Prefix_12_0 (e.1) (e.2)>;
 (t.1) (e.1) (e.2 t.2) (e.3) =  <Prefix_0 (t.1) (e.1) (e.2) (t.2 e.3)>;
}
*/

/*
 В остаточной программе не оказалось ветвей, возвращающих 'F', за счет того, что выход из функции Concat совпадает с первым аргументом функции Prefix.
 Здесь уравнения в словах не сыграли никакой роли.
*/
