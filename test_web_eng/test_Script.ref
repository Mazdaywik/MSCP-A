
/*
 Тест взят из статьи 2016 года на CAV:
 M.T Trinh, D.H Chu, J. Jaffar, Progressive reasoning over recursively-defined strings /
 International Conference on Computer Aided Verification, 218-240

 Процедура аудита заменяет все вхождения в строку подстроки 'ip' на подстроку ' ip address '.
 Требуется установить корректность данной процедуры аудита: а именно, что после нее в строке не будет подстроки '<script'.
 Функция ReplaceIp осуществляет указанную подстановку, функция Check проверяет условие корректности.
  
 Остаточная программа после суперкомпиляции не содержит выходов, возвращающих 'F'.
*/


/***********************************************/
/*          исходная программа                 */
/***********************************************/

$ENTRY Go {
   e.p = <Check <ReplaceIp e.p>>;
}

ReplaceIp {
    e.1 'ip' e.2 = e.1' ip address '<ReplaceIp e.2>;
    e.Other = e.Other;
}

Check {
    = 'T';
    '<script'e.2 = 'F';
    t.1 e.Z = <Check e.Z>;
}

/***********************************************/
/*          остаточная программа               */
/***********************************************/

/*

$ENTRY Go {
 e.1 =  <Check_0 e.1>;
}


Check_0 {
 e.1 'ip' e.2 =  <Check_3_0 (e.1) (e.2)>;
 e.1 =  <Check_13_0 e.1>;
}


Check_3_0 {
 (t.1 e.1) (e.2) =  <Check_3_0 (e.1) (e.2)>;
 () (e.1) =  <Check_0 e.1>;
}


Check_13_0 {
 =  'T';
 t.1 e.1 =  <Check_13_0 e.1>;
}

*/
