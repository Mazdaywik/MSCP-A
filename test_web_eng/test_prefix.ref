/*
 Функция Prefix проверяет, является ли ее первый аргумент префиксом второго, в алфавите {'A','B'}.
 Функция Concat --- ассоциативное приписывание.
 Задача суперкомпилятора --- проверить, всегда ли с его помощью получается префикс.
*/

/***********************************************/
/*          исходная программа                 */
/***********************************************/

$ENTRY Go {
	(e.x) e.y = <Prefix (e.x) <Concat (e.x) e.y>>;
}

Prefix {
	() e.2 = 'T';
	(t.x e.1) t.x e.2 = <Prefix (e.1) e.2>;
	('A' e.1) 'B' e.2 = 'F';
	('B' e.1) 'A' e.2 = 'F';
	(t.x e.1) = 'F';
}

Concat {
	(e.1) e.2 = e.1 e.2;
}

/***********************************************/
/*          остаточная программа               */
/***********************************************/
/*
$ENTRY Go {
 (e.1) e.2 =  <InputFormat_0 (e.1) (e.2)>;
}


InputFormat_0 {
 () (e.1) =  'T';
 (t.1 e.1) (e.2) =  <Prefix_12_0 (e.1) (e.2)>;
}


Prefix_12_0 {
 () (e.1) =  'T';
 (t.1 e.1) (e.2) =  <Prefix_12_0 (e.1) (e.2)>;
}
*/

/*
 Функция Concat приписывания существенно использует встроенную ассоциативность входного языка.
 За счет этого верификация проходит успешно.
*/
