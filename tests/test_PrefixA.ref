/*
 Функция Prefix проверяет, является ли ее первый аргумент префиксом второго, в алфавите {A,B}.
 Функция Concat --- ассоциативное приписывание.

 Задача суперкомпилятора --- проверить, в каких случаях e.x будет префиксом слова Concat('A'e.x, e.y).
*/

/***********************************************/
/*          исходная программа                 */
/***********************************************/
$ENTRY Go {
	 (e.x) e.y = <Prefix (e.x) <Concat ('A' e.x) e.y>>;
}

Prefix {
	() e.2 = 'T';
	(t.1 e.1) t.1 e.2 = <Prefix (e.1) e.2>;
	('A' e.1) 'B' e.2 = 'F';
	('B' e.1) 'A' e.2 = 'F';
	(t.1 e.1) = 'F';
}

Concat {
	(e.1) e.2 = e.1 e.2;
}

/***********************************************/
/*          остаточная программа               */
/***********************************************/
/*
$ENTRY Go {
 (e.1) e.2 =  <InputFormat_0 (e.1) (e.2)>;
}


InputFormat_0 {
 () (e.1) =  'T';
 ('A' e.1) (e.2) =  <Prefix_124_0 (e.1) (e.2)>;
 ('B' e.1) (e.2) =  'F';
}


Prefix_124_0 {
 () (e.1) =  'T';
 ('A' e.1) (e.2) =  <Prefix_124_0 (e.1) (e.2)>;
 ('B' e.1) (e.2) =  'F';
}
*/

/*
 Цикл Prefix_124_0 остаточной программы отвечает на вопрос, поставленный в задаче:
 префиксом слова 'A' e.x e.y слово e.x будет тогда и только тогда, когда слово e.x само состоит из одних букв 'А'.
*/